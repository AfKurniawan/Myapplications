apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
   // buildToolsVersion "26.0.1"

    defaultConfig {
        applicationId "com.androprogrammer.tutorials"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables{
            useSupportLibrary = true
        }
    }
    sourceSets{
        main{
            res.srcDirs = [
                'src/main/res',
                'src/main/res/layout/fragments',
                'src/main/res/layout/adapter_itemview',
                'src/main/res/layout/tutorials_view'
            ]
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }
    dexOptions{
        preDexLibraries true
        javaMaxHeapSize "2g"
    }
    lintOptions {
        abortOnError false
        absolutePaths false
    }
    compileOptions {
        incremental false
    }

    configurations.all {
        resolutionStrategy.force 'com.android.support:support-annotations:23.0.1'
    }
}

ext {
    supportLibVersion = '27.1.0'
    playServiceLibVersion = '9.8.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    wearApp project(':wear')

    // For app compatibility
    implementation "com.android.support:support-v4:${supportLibVersion}",
        "com.android.support:support-v13:${supportLibVersion}",
        "com.android.support:appcompat-v7:${supportLibVersion}",
        "com.android.support:design:${supportLibVersion}",
        "com.android.support:recyclerview-v7:${supportLibVersion}",
        "com.android.support:cardview-v7:${supportLibVersion}"

    // For google services
    implementation "com.google.android.gms:play-services-maps:${playServiceLibVersion}",
        "com.google.android.gms:play-services-location:${playServiceLibVersion}",
        "com.google.android.gms:play-services-wearable:${playServiceLibVersion}",
        "com.google.android.gms:play-services-vision:${playServiceLibVersion}"

    // For 65k methods
    implementation 'com.android.support:multidex:1.0.1'

    implementation 'com.google.code.gson:gson:2.7'
    implementation 'com.koushikdutta.ion:ion:2.1.6'
    implementation 'com.anupcowkur:reservoir:2.1'

    annotationProcessor 'com.jakewharton:butterknife:7.0.1'
    implementation 'com.jakewharton:butterknife:7.0.1'

    // For custom radio button
    implementation 'com.github.worker8:RadioGroupPlus:v1.0.1'

    // For espresso test cases
    androidTestImplementation ('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    
    androidTestImplementation ('com.android.support.test.espresso:espresso-contrib:2.0') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
    androidTestImplementation "com.android.support.test:rules:1.0.2"
    androidTestImplementation "com.android.support.test:runner:1.0.2"
}
